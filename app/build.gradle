buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

android {
    compileSdkVersion 27
    dataBinding.enabled = true

    lintOptions {
        checkReleaseBuilds false
    }

    Properties properties = new Properties()
    File releasePropsFile = rootProject.file('.signing/release.properties')
    if (releasePropsFile.exists()) {
        /*
        The release.properties file should look like this:
        mapsApiKey=...
        keystoreFile=path/to/keystore
        keystorePassword=...
        keyAlias=...
        keyPassword=...
         */
        properties.load(new FileInputStream(releasePropsFile))
    }

    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        applicationId "speakman.whatsshakingnz"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 13
        versionName "2.0.3"
        manifestPlaceholders = [mapsApiKey: properties['mapsApiKey']]
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            if (properties['keystoreFile'] != null) {
                storeFile rootProject.file(properties['keystoreFile'])
                storePassword properties['keystorePassword']
                keyAlias properties['keyAlias']
                keyPassword properties['keyPassword']
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.debug
            ext.enableCrashlytics = false
        }
        debugAnalytics {
            debuggable true
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    kapt "com.android.databinding:compiler:$gradle_version"

    def supportLibVersion = '26.1.0'
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:support-vector-drawable:$supportLibVersion"

    def playServicesVersion = '11.8.0'
    implementation "com.google.android.gms:play-services-maps:$playServicesVersion"
    implementation "com.google.android.gms:play-services-gcm:$playServicesVersion"
    implementation 'com.firebase:firebase-jobdispatcher:0.8.5'

    def daggerVersion = '2.14.1'
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    def leakCanaryVersion = '1.5.4'
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    debugAnalyticsImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    def okHttpVersion = '3.9.1'
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okHttpVersion"

    implementation "com.google.code.gson:gson:2.8.2"
    implementation "io.reactivex:rxjava:1.3.6"
    implementation "io.reactivex:rxandroid:1.2.1"
    // TODO Need this for PowerMock+Realm right now, upgrade to actually use it!
    testImplementation "io.reactivex.rxjava2:rxjava:2.1.10"
    implementation "net.danlew:android.joda:2.9.9.2"
    implementation "com.jakewharton.timber:timber:4.1.2"
    implementation "com.jonathanfinerty.once:once:1.2.2"
    implementation("com.crashlytics.sdk.android:crashlytics:2.9.0@aar") {
        transitive = true;
    }

    // For Tests
    testImplementation 'joda-time:joda-time:2.9.9'
    testImplementation 'junit:junit:4.12'
    testImplementation "com.nhaarman:mockito-kotlin:1.5.0"
    testImplementation 'org.powermock:powermock-module-junit4:1.7.3'
    testImplementation 'org.powermock:powermock-api-mockito2:1.7.3'

    // Android Tests
    androidTestImplementation "com.android.support:support-annotations:$supportLibVersion"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
}
