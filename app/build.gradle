buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 26
    dataBinding.enabled = true

    lintOptions {
        checkReleaseBuilds false
    }

    Properties properties = new Properties()
    File releasePropsFile = rootProject.file('.signing/release.properties')
    if (releasePropsFile.exists()) {
        /*
        The release.properties file should look like this:
        mapsApiKey=...
        keystoreFile=path/to/keystore
        keystorePassword=...
        keyAlias=...
        keyPassword=...
         */
        properties.load(new FileInputStream(releasePropsFile))
    }

    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        applicationId "speakman.whatsshakingnz"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 13
        versionName "2.0.3"
        manifestPlaceholders = [ mapsApiKey: properties['mapsApiKey'] ]
    }

    signingConfigs {
        release {
            if (properties['keystoreFile'] != null) {
                storeFile rootProject.file(properties['keystoreFile'])
                storePassword properties['keystorePassword']
                keyAlias properties['keyAlias']
                keyPassword properties['keyPassword']
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.debug
            ext.enableCrashlytics = false
        }
        debugAnalytics {
            debuggable true
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    def supportLibVersion = '26.1.0'
    def playServicesVersion = '11.8.0'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:support-vector-drawable:$supportLibVersion"
    implementation "com.google.android.gms:play-services-maps:$playServicesVersion"
    implementation "com.google.android.gms:play-services-gcm:$playServicesVersion"
    implementation "com.squareup.okhttp3:okhttp:3.9.1"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:3.9.1"
    implementation "com.google.code.gson:gson:2.8.2"
    implementation "io.reactivex:rxjava:1.1.1"
    implementation "io.reactivex:rxandroid:1.1.0"
    implementation "net.danlew:android.joda:2.9.9.2"
    implementation "io.realm:realm-android:0.87.4"
    annotationProcessor "io.realm:realm-android:0.87.4"
    implementation "com.google.dagger:dagger:2.4"
    implementation "com.jakewharton.timber:timber:4.1.2"
    implementation "com.jonathanfinerty.once:once:1.2.2"
    annotationProcessor "com.google.dagger:dagger-compiler:2.4"
    // Oh dear https://github.com/google/dagger/issues/356
    annotationProcessor "com.google.guava:guava:19.0"
    androidTestCompile("org.mockito:mockito-core:1.10.19") {
        exclude group: "org.hamcrest"
    }
    androidTestImplementation "com.google.dexmaker:dexmaker:1.2"
    androidTestImplementation "com.google.dexmaker:dexmaker-mockito:1.2"
    implementation("com.crashlytics.sdk.android:crashlytics:2.9.0@aar") {
        transitive = true;
    }
    def leakCanaryVersion = '1.5.4'
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    debugAnalyticsImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
}
